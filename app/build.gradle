
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId 'com.ibis.notes'
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName '1.0'
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        // We use a bundled debug keystore, to allow debug builds from CI to be upgradable
        debug {
        }
    }

    buildTypes {
        debug {
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        allWarningsAsErrors = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.compose
    }

    packagingOptions {
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }
}

dependencies {
    implementation project(':markdown')
    implementation project(':ui-editor')
    implementation project(':domain')
    implementation project(':ui-common')
    implementation project(':ui-notes')
    implementation project(':ui-note-detail')
    implementation project(':ui-search')
    implementation project(':ui-auth')

    implementation platform(Libs.Firebase.bom)
    implementation Libs.Firebase.crashlytics
    implementation Libs.Firebase.analytics
    implementation Libs.Firebase.firestore

    implementation Libs.AndroidX.Lifecycle.livedata
    implementation Libs.AndroidX.Lifecycle.viewmodel

    implementation Libs.AndroidX.Activity.compose

    implementation Libs.Retrofit.serialization
    implementation Libs.Serialization.runtime
    implementation 'androidx.lifecycle:lifecycle-process:2.3.1'

    kapt Libs.Moshi.compiler

    implementation Libs.AndroidX.emoji
    implementation Libs.AndroidX.Fragment.fragmentKtx

    implementation Libs.AndroidX.Navigation.compose

    implementation Libs.AndroidX.Compose.foundation
    implementation Libs.AndroidX.Compose.ui
    implementation Libs.AndroidX.Compose.layout
    implementation Libs.AndroidX.Compose.material
    implementation Libs.AndroidX.Compose.animation
    implementation Libs.AndroidX.Compose.tooling
    implementation Libs.AndroidX.Compose.livedata

    implementation Libs.Accompanist.coil
    implementation Libs.Accompanist.insets
    implementation Libs.Accompanist.flowLayout
    implementation Libs.Accompanist.pager
    implementation Libs.Accompanist.systemuicontroller

    implementation Libs.threeTenAbp
    implementation Libs.dataStore

    implementation Libs.timber
    api Libs.store

    implementation Libs.Kotlin.stdlib

    implementation Libs.Coroutines.core
    implementation Libs.Coroutines.android
    implementation Libs.Coroutines.play

    implementation Libs.Hilt.library
    kapt Libs.Hilt.compiler

    implementation Libs.AndroidX.Hilt.work
    implementation Libs.AndroidX.Hilt.navigation
    implementation Libs.AndroidX.Hilt.compose
    kapt Libs.AndroidX.Hilt.compiler
}

android.applicationVariants.all { variant ->
    task "open${variant.name.capitalize()}" {
        dependsOn "install${variant.name.capitalize()}"

        doLast {
            exec {
                commandLine "adb shell monkey -p ${variant.applicationId} -c android.intent.category.LAUNCHER 1".split(" ")
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
